buildscript {
    repositories {
        google()
        maven { url 'https://maven.fabric.io/public' }
        jcenter()

    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0-beta05'
        classpath 'io.fabric.tools:gradle:1.27.1'
    }
}

def generateVersionNumberString = { ->
    StringBuilder stringBuilder = new StringBuilder()
    stringBuilder.append((new Date()).format('yyMMddHHmm'))
    return stringBuilder.toString()
}

def generateTimestamp = { ->
    StringBuilder stringBuilder = new StringBuilder()
    stringBuilder.append((new Date()).getTime())
    stringBuilder.append("L")
    return stringBuilder.toString()
}

def generateRandomUUID = { ->
    StringBuilder stringBuilder = new StringBuilder()
    stringBuilder.append('"' + UUID.randomUUID().toString() + '"')
    return stringBuilder.toString()
}

def generateVersionName = { ->

    StringBuilder stringBuilder = new StringBuilder()
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--always'
            standardOutput = stdout
        }
        String commitObject = stdout.toString().trim()
        try {
            stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'describe', '--tags'
                standardOutput = stdout
            }
            //stringBuilder.append(stdout.toString().trim())
            //stringBuilder.append("-")
        } catch (ignored) {
            // no tags
        }

        stringBuilder.append(commitObject)
        stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--show-toplevel'
            standardOutput = stdout
        }
        if (stdout.toString().trim().contains("xDrip-Experimental")) {
            stringBuilder.append("-experimental")
        }

        stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
            standardOutput = stdout
        }
        String branch = stdout.toString().trim()
        if (!branch.equals("master")) {
            stringBuilder.append('-')
            stringBuilder.append(branch)
        }

    } catch (ignored) {
        return "NoGitSystemAvailable"
    }
    stringBuilder.append('-')
    stringBuilder.append((new Date()).format('yyyy.MM.dd'))
    return stringBuilder.toString()

}

apply plugin: 'com.android.application'
//apply plugin: 'me.tatarka.retrolambda'
//apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    lintOptions {
        checkReleaseBuilds false
//        abortOnError false
    }
    defaultConfig {
        applicationId "com.eveningoutpost.dexdrip"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName generateVersionName()
        buildConfigField "int", "buildVersion", generateVersionNumberString()
        buildConfigField "String", "buildUUID", generateRandomUUID()
        buildConfigField "long", "buildTimestamp", generateTimestamp()
        buildConfigField "int", "targetSDK", targetSdkVersion.mApiLevel.toString()

        applicationId = "com.eveningoutpost.dexdrip"
        resValue "string", "label_xdrip", "XDrip"
        resValue "string", "label_xdrip_large", "XDrip(Large)"
        resValue "string", "label_xdrip_big_chart", "XDrip(BigChart)"
        resValue "string", "label_xdrip_circle", "XDrip(Circle)"
        resValue "string", "label_xdrip_activity", "XDrip Prefs."
        resValue "string", "app_settings", "XDrip Settings"

    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility '1.8'
        targetCompatibility '1.8'
    }

    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

allprojects {
    repositories {
        google()
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    compileOnly 'com.google.android.wearable:wearable:2.4.0'
    implementation 'com.google.android.support:wearable:2.4.0'
//    implementation 'com.android.support:appcompat-v7:28.0.0'
    // implementation 'com.google.android.support:wearable:1.4.0'
    implementation 'com.google.android.gms:play-services-wearable:16.0.1'
    implementation 'com.squareup.wire:wire-runtime:2.3.0-RC1'
    //implementation 'com.ustwo.android:clockwise-wearable:1.0.2'
    //implementation files('libs/hellocharts-library-1.5.5.jar')
    implementation(name: 'ustwo-clockwise-debug', ext: 'aar')
    implementation 'me.denley.wearpreferenceactivity:wearpreferenceactivity:0.5.0'
    implementation 'com.google.code.gson:gson:2.8.5'

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.9@aar') {
       transitive = true
    }

    //implementation 'io.reactivex:rxjava:1.3.2'
    implementation 'com.activeandroid:thread-safe-active-android:3.1.1'
    implementation 'com.google.guava:guava:27.0.1-jre'
    implementation 'io.reactivex:rxjava:1.3.8'
    implementation 'com.polidea.rxandroidble:rxandroidble:1.7.0'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    testImplementation 'org.robolectric:robolectric:4.2'
    testImplementation 'junit:junit:4.13-beta-2'
    testImplementation 'com.google.truth:truth:0.43'

    implementation 'uk.com.robust-it:cloning:1.9.12'
    // you will want to install the android studio lombok plugin
    compileOnly 'org.projectlombok:lombok:1.18.6'
//    compileOnly 'javax.annotation:javax.annotation-api:1.3.1'
    annotationProcessor "org.projectlombok:lombok:1.18.6"
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha02'

    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0-alpha01'
}
